import java.nio.file.Files
import java.nio.file.Paths

apply plugin: 'net.minecraftforge.gradle'

configurations {
    mod
}

minecraft {
    mappings channel: mc_mappings_channel, version: mc_mappings_version
}

ext.shadePackages = { String... packages ->
    // nop
}
allmodules.each {
    if(file("$it.projectDir/prebuild.gradle").exists()) {
        apply from: "$it.projectDir/prebuild.gradle"
    }
}

dependencies {
    minecraft("net.minecraftforge:forge:$mc_version-$forge_version")
    configurations.findByName("minecraft").exclude(group: 'com.ibm.icu', module: 'icu4j-core-mojang')

    implementation "thedarkcolour:kotlinforforge:$kotlinforforge_version"

    allmodules.each {
        runtimeOnly project(path: it.path, configuration: 'devClasspath')
    }
}

// we need all the modules to compile first
allmodules.each {
    classes.dependsOn(it.classes)
    processResources.dependsOn(it.processResources)
    classes.dependsOn(it.testClasses)
    processResources.dependsOn(it.processTestResources)
}

// Create empty directories for all of the build outputs.
// ForgeGradle gives all the output directories to forge to search, but when the directories don't exist forge spews
// dozens of errors about missing directories when searching for mods:
//   [18:17:04] [pool-2-thread-1/ERROR] [ne.mi.fm.lo.mo.ExplodedDirectoryLocator/SCAN]: Exception scanning <<project dir>>/modules/utilities/build/classes/java/test
//   java.nio.file.NoSuchFileException: <<project dir>>/modules/utilities/build/classes/java/test
//       at sun.nio.fs.UnixException.translateToIOException(UnixException.java:86) ~[?:1.8.0_181] {}
task(createEmptyOutputDirs).doLast {
    allmodules.each { module ->
        module.sourceSets.main.output.each {
            it.mkdirs()
        }
        module.sourceSets.test.output.each {
            it.mkdirs()
        }
    }
}

def link(File link, File target) {
    // create the target first. a symlink to a non-existent directory will return false from .exists(), but will crash
    // if we create its target and try to create a new link, since it now "exists"
    target.mkdirs()
    if(link.exists())
        return
    link.parentFile.mkdirs()
    Files.createSymbolicLink(
            Paths.get(link.absolutePath),
            Paths.get(target.absolutePath)
    )
}

task(createDataGenSymlinks).doLast {
    allmodules.each { mod ->
        link(file("run/datagen/$mod.modid/assets"), mod.file('src/main/datagen/assets'))
        link(file("run/datagen/$mod.modid_test/assets"), mod.file('src/test/datagen/assets'))
        link(file("run/datagen/$mod.modid/data"), mod.file('src/main/datagen/data'))
        link(file("run/datagen/$mod.modid_test/data"), mod.file('src/test/datagen/data'))
    }
}

task copyLibModJars(type: Sync) {
    into project.file('run/mods')
    from project.file('mods')
    from configurations.mod

    allmodules.each {
        from it.configurations.mod
    }
}
compileJava.dependsOn(copyLibModJars)
compileJava.dependsOn(rootProject.updateReadmeVersions)

minecraft {
    Closure runDefaults = {
        workingDirectory project.file('run')
        // ForgeGradle would generate `runtime_main`, which is wrong on two counts. Firstly it needs to be `.main` and
        // not `_main` (which is a _known issue_ https://github.com/MinecraftForge/ForgeGradle/issues/425), and secondly
        // it doesn't take into account the fact that this project may not be the root project.
        // https://github.com/MinecraftForge/ForgeGradle/blob/bd92a0d384b987be361ed3f7df28b1980f7fae1e/src/common/java/net/minecraftforge/gradle/common/util/RunConfig.java#L240
        ideaModule 'librarianlib.runtime.main'
        singleInstance true
        taskName name

        property 'forge.logging.markers', 'REGISTRIES'
        property 'forge.logging.console.level', 'debug'

        mods {
            allmodules.each { mod ->
                create(mod.modid) {
                    sources mod.sourceSets.main
                }
                create(mod.modid_test) {
                    source mod.sourceSets.test
                }
            }
        }
    }

    runs {
        client {
            configure(delegate, runDefaults)
        }

        server {
            configure(delegate, runDefaults)
        }

        data {
            configure(delegate, runDefaults)
            jvmArg('-XstartOnFirstThread')
            def existing = allmodules.collect { mod ->
                [
                        '--existing', '"' + mod.sourceSets.main.output.resourcesDir.absolutePath + '"',
                        '--existing', '"' + mod.sourceSets.test.output.resourcesDir.absolutePath + '"'
                ]
            }.flatten()
            args(['--all', '--output', project.file('run/datagen')] + [
                    '--mod', allmodules.collect { mod -> "$mod.modid,$mod.modid_test" }.join(',')
            ] + existing)
        }
    }
}

// I have to create the directories at the last moment, otherwise for some reason something else ends up deleting them
tasks.whenTaskAdded { task ->
    if(["prepareClient", "prepareServer", "prepareData"].contains(task.name))
        task.finalizedBy(createEmptyOutputDirs)
    if(task.name == "prepareData")
        task.finalizedBy(createDataGenSymlinks)
}
